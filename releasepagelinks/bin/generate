#!/usr/bin/perl
#
# Generate an HTML page that has the upstream release page links in it
#

use strict;
use warnings;

use Getopt::Long;

use File::Find;
use UBOS::Logging;
use UBOS::Utils;

my $verbose       = 0;
my $logConfigFile = undef;
my @dirs;
my $out;
my $help          = 0;

my $parseOk = GetOptions(
        'verbose+'    => \$verbose,
        'logConfig=s' => \$logConfigFile,
        'out=s'       => \$out,
        'h|help'      => \$help );

UBOS::Logging::initialize( 'releasepagelinks', 'generate', $verbose, $logConfigFile, 0 );

if( @ARGV ) {
    @dirs = @ARGV;
}
if( !$parseOk ) {
    synopsisHelpQuit( 0 );
}
if( $help ) {
    synopsisHelpQuit( 1 );
}

unless( @dirs ) {
    @dirs = ( '.' );
}

my %dirsWithPkgbuilds = ();
my $projects          = (); # 'ubos-wordpress' -> 'wordpress' -> data

find(
        sub {
            my $name = $_;
            if( $name eq 'PKGBUILD' ) {
                $dirsWithPkgbuilds{$File::Find::dir} = 1;
            }
        },
        @dirs );

info( "Found dirs with PKGBUILDS:", keys %dirsWithPkgbuilds );

foreach my $dirWith ( keys %dirsWithPkgbuilds ) {
    my $out;
    UBOS::Utils::myexec(
            "cd '$dirWith' && /bin/bash",
            ". 'PKGBUILD'; echo pkgname=\$pkgname ; echo pkgver=\$pkgver ; echo pkgverforked=\$pkgverforked ; echo releasepage=\${releasepage[@]}",
            \$out );

    my %found = ();
    foreach my $line( split /\n/, $out ) {
        if( $line =~ m!^([^=]+)=(.+)$! ) { # ignore keys that don't have a value
            if( $1 eq 'releasepage' ) {
                $found{$1} = [ split( /\s/, $2 ) ];
            } else {
                $found{$1} = $2;
            }
        }
    }
    if( exists( $found{releasepage} )) {
        my $projectName = $dirWith;
        $projectName =~ s!^\.\.?/!!;
        $projectName =~ s!/.*!!;
        $projects->{$projectName}->{$found{pkgname}} = \%found;
    }
}

my ( $sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst ) = gmtime( time() );
my $tstamp = sprintf "%.4d-%.2d-%.2d %.2d:%.2d:%.2d", ($year+1900), ( $mon+1 ), $mday, $hour, $min, $sec;

my $html = <<HTML;
<html>
 <head>
  <title>UBOS upstream packages to check</title>
  <style>
body {
    margin: 0;
    padding: 10px 20px;
    font-family: sans-serif;
}
table.packages {
    width: 100%;
    border-collapse: collapse;
}
thead, tbody {
    border: 2px solid #404040;
}
td, th {
    border: 1px solid #a0a0a0;
    padding: 5px;
}

  </style>
 </head>
 <body>
  <h1>UBOS upstream packages to check</h1>
  <p>As of $tstamp</p>
  <table class="packages">
   <thead>
    <tr>
     <th>Project name</th>
     <th>#</th>
     <th>Package name</th>
     <th>UBOS has version</th>
     <th>Upstream version forked</th>
     <th>Release page upstream</th>
    </tr>
   </thead>
HTML

my $i = 0;
foreach my $projectName ( sort keys %$projects ) {
    $html .= "    <tbody>\n";
    foreach my $packageName ( sort keys %{$projects->{$projectName}} ) {
        my $packageData = $projects->{$projectName}->{$packageName};

        $html .= "     <tr>\n";
        $html .= "      <td>$projectName</td>\n";
        $html .= "      <td style=\"text-align: right\">$i</td>\n";
        $html .= "      <td>$packageName</td>\n";
        if( exists( $packageData->{pkgver} )) {
            $html .= "      <td>$packageData->{pkgver}</td>\n";
        } else {
            $html .= "      <td>(no pkgver)</td>\n";
        }
        if( exists( $packageData->{pkgverforked} )) {
            $html .= "      <td>$packageData->{pkgverforked}</td>\n";
        } else {
            $html .= "      <td>(no pkgverforked)</td>\n";
        }
        if( exists( $packageData->{releasepage} )) {
            $html .= "      <td>" . join( "<br>", map { "<a href='$_'>visit</a>" } sort @{$packageData->{releasepage}} ) . "</td>\n";
        } else {
            $html .= "      <td>(no releasepage)</td>\n";
        }
        $html .= "     </tr>\n";
        ++$i;
    }
    $html .= "    </tbody>\n";
}

$html .= <<HTML;
  </table>
  <p>The end.</p>
 </body>
</html>
HTML

if( $out ) {
    UBOS::Utils::saveFile( $out, $html );
} else {
    print $html;
}
exit 0;

#####
# Print synopsis or help
# $long: if true, print long help
# $exit: exit code
sub synopsisHelpQuit {
    my $long = shift;
    my $exit = shift || 0;

    if( $long ) {
        print <<END;
Generate an HTML page that has links to all upstream packages' release pages, and current versions in UBOS>
END
    }
    print "Synopsis:\n";
    print "    $0 [--out <outfile>]  [<dir>]... \n";
    if( $long ) {
        print <<END;
    <out> -- filename of the HTML output file, defaults to stdout
    <dir> -- the start directories from which PKGBUILD files will be searched, defaults to the current directory
END
    }
    exit 0;
}

1;

